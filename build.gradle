buildscript {
    repositories {
        jcenter()
        maven {
            url 'http://repo.jenkins-ci.org/releases/'
        }
    }
    dependencies {
        classpath 'org.jenkins-ci.tools:gradle-jpi-plugin:0.14.0'
        classpath 'com.netflix.nebula:nebula-release-plugin:3.1.0'
    }
}

apply plugin: 'groovy'
apply plugin: 'org.jenkins-ci.jpi'

description = 'Plugin to receive SCM change notifications via RabbitMQ and triggering builds'
group = 'com.aoe.jenkins'

// keep the generated sources outside of the build directory, see http://stackoverflow.com/a/21003914/1271460
def generatedSourcesDir = 'generated'

jenkinsPlugin {
    coreVersion = '1.600'
    displayName = 'SCM Push Trigger'
    //url = ''
    //gitHubUrl = ''
    repoUrl = 'https://extranet.aoe.com/artifactory/libs-release-local'
    snapshotRepoUrl = 'https://extranet.aoe.com/artifactory/libs-snapshot-local'
    fileExtension = 'jpi'
    configureRepositories = false
    localizerOutputDir = "$generatedSourcesDir/localizer"
    developers {
        developer {
            id 'carstenlenz'
            name 'Carsten Lenz'
            email 'carsten.lenz@aoe.com'
        }
    }
}

repositories {
    jcenter()
    maven {
        url 'http://repo.jenkins-ci.org/releases/'
    }
}

clean {
    delete generatedSourcesDir
    delete 'classes'
}

test {
    // workaround for JENKINS-4409 to avoid leaking files into the system temp directory
    systemProperty 'java.io.tmpdir', temporaryDir.absolutePath
}

dependencies {
    compile 'org.codehaus.groovy:groovy-all:1.8.9'
    jenkinsPlugins 'org.jenkins-ci.plugins:rabbitmq-consumer:2.7@jar'
    jenkinsPlugins 'org.jenkins-ci.plugins:git:2.4.0@jar'

    // Don't know why but since rabbitmq-consumer 2.5 -> 2.7 this is required
    optionalJenkinsPlugins 'org.slf4j:slf4j-api:1.7.7'
    optionalJenkinsPlugins 'org.jenkins-ci.plugins:multiple-scms:0.5@jar'
    optionalJenkinsPlugins 'org.jenkins-ci.plugins:job-dsl:1.40@jar'
    optionalJenkinsPlugins 'org.jenkins-ci.plugins:job-dsl-core:1.40@jar'

    testCompile 'org.spockframework:spock-core:0.7-groovy-1.8'
    testCompile 'junit:junit-dep:4.10'
    testCompile 'cglib:cglib-nodep:2.2.2' // used by Spock
    testCompile 'org.objenesis:objenesis:1.2' // used by Spock
}

// As long as we don't deploy to jenkins-ci.org we use our auth standards
gradle.taskGraph.whenReady { taskGraph ->
    PublishingExtension publishingExtension = project.extensions.getByType(PublishingExtension)
    publishingExtension.repositories.getByName('jenkins').credentials {
        username = project.hasProperty('aoeRepoUsername') ? aoeRepoUsername : ''
        password = project.hasProperty('aoeRepoPassword') ? aoeRepoPassword : ''
    }
}